buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:2.1.21"
    implementation "androidx.core:core:1.9.0"
    implementation "androidx.legacy:legacy-support-core-utils:1.0.0"
    implementation "androidx.appcompat:appcompat:1.5.1"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    implementation "androidx.browser:browser:1.4.0"
    implementation "androidx.annotation:annotation:1.5.0"
    implementation "org.mozilla.geckoview:geckoview:140.0.20250707120347"
    implementation "cz.msebera.android:httpclient:4.5.8"
    implementation fileTree(dir: 'libs', include: ['*.jar'])
}

android {
    namespace 'com.manuelmaly.hn'
    compileOptions {
        encoding "UTF-8"
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    compileSdkVersion 35
    buildFeatures {
        viewBinding true
    }
    defaultConfig {
        applicationId "com.manuelmaly.hn"
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 1
        versionName project.hasProperty("versionName") ? project.property("versionName") : "0.0.0"
    }
    signingConfigs {
        def props = new Properties()
        props.load(new FileInputStream(rootProject.file("release.properties")))
        release {
            storeFile new File(props.keyStore.toString())
            storePassword props.keyStorePassword
            keyAlias props.keyAlias
            keyPassword props.keyAliasPassword
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFileName
                    if (outputFile != null && outputFile.endsWith('.apk')) {
                        def fileName = outputFile.replace('.apk',
                                "-v${versionName}-b${versionCode}.apk")
                        output.outputFileName = fileName
                    }
                }
            }
        }
        debug {
        }
    }
    sourceSets {
        main.java.srcDirs = ['src/main/kotlin']
    }
}

repositories {
    mavenCentral()
    maven {
        url "https://maven.mozilla.org/maven2/"
    }
}
