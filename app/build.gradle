buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:2.1.21"
    implementation "androidx.core:core:1.9.0"
    implementation "androidx.legacy:legacy-support-core-utils:1.0.0"
    implementation "androidx.appcompat:appcompat:1.5.1"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    implementation "androidx.browser:browser:1.4.0"
    implementation "androidx.annotation:annotation:1.5.0"
    implementation "org.mozilla.geckoview:geckoview:140.0.20250707120347"
    implementation "cz.msebera.android:httpclient:4.5.8"
    implementation fileTree(dir: 'libs', include: ['*.jar'])
}

android {
    namespace 'com.sergebakharev.hnplus'
    compileOptions {
        encoding "UTF-8"
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    compileSdkVersion 35
    buildFeatures {
        viewBinding true
    }
    defaultConfig {
        applicationId "com.sergebakharev.hnplus"
        minSdkVersion 21
        targetSdkVersion 35
        versionCode 8
        versionName project.hasProperty("versionName") ? project.property("versionName") : "0.0.0"
    }
    signingConfigs {
        def props = new Properties()
        if (rootProject.file("release.properties").exists()) {
            props.load(new FileInputStream(rootProject.file("release.properties")))
        }
        release {
            storeFile props.keyStore ? new File(rootProject.projectDir, props.keyStore.toString()) : null
            storePassword props.keyStorePassword ?: ""
            keyAlias props.keyAlias ?: ""
            keyPassword props.keyAliasPassword ?: ""
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            debuggable false
            shrinkResources false
        }
        debug {
        }
    }
    
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFileName
            if (outputFile != null && outputFile.endsWith('.aab')) {
                def fileName = "hnplus-${variant.buildType.name}-${versionName}.aab"
                output.outputFileName = fileName
            }
        }
    }
    sourceSets {
        main.java.srcDirs = ['src/main/kotlin']
    }
}

// Task to copy NOTICE file to assets
task copyNoticeFile(type: Copy) {
    from rootProject.file('NOTICE')
    into file('src/main/assets')
    rename { String fileName ->
        'NOTICE'
    }
}

// Make sure the copy task runs before the build
preBuild.dependsOn copyNoticeFile

repositories {
    mavenCentral()
    maven {
        url "https://maven.mozilla.org/maven2/"
    }
}
